#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#


property newServerAcceptRef ${nukleus:newRouteRef()}
property newClientConnectRef ${newServerAcceptRef}

property serverAccept "nukleus://socks/streams/source"
property boundAddress ${socks:socksAddress()
                             .localhostIpv4()
                             .port(8080)
                             .build()}

accept ${serverAccept}
  option nukleus:route ${newClientConnectRef}
  option nukleus:window 65536
  option nukleus:transmission "duplex"
accepted
connected

## Read the request bytes for connecting with no-authentication
read [0x05 0x01 0x00]

## Write the response, expect success
write [0x05 0x00]

## Read CONNECT request
read [0x05] # version: SOCKS v5
read [0x01] # CONNECT command
read [0x00] # RSV field
read ${socks:socksAddress()
            .domainName("localhost")
            .port(8080)
            .build()}

## Write CONNECT response
write [0x05] # version: SOCKS v5
write [0x00] # Succeeded
write [0x00] # RSV field
write ${boundAddress}

## Application data
read "hello world"
write "hello client"

## Application data
read "data flows as intended"
write "data flows back as intended"

